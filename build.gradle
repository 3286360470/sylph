plugins {
  id "com.github.hierynomus.license" version "0.16.1"
  id "com.github.harbby.gradle.serviceloader" version "1.1.8"
  id "jacoco"
  id "idea"
}

allprojects {
  ext.deps = [
          hadoop              : "3.2.1",
          spark               : "3.2.0",
          flink               : '1.14.3',
          jetty               : "11.0.6",
          scala               : '2.12.15',
          joda_time           : '2.9.3',
          slf4j               : '2.0.0-alpha1',
          gadtry              : '1.9.9',
          guava               : '31.0.1-jre',
          jackson             : '2.12.2',
          jersey              : '3.0.3',
  ]
}

subprojects {
  apply plugin: 'java'
  group 'com.github.harbby'
  version '1.0.0-SNAPSHOT'  //SNAPSHOT

  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'checkstyle'
  apply plugin: 'jacoco'
  apply plugin: 'com.github.hierynomus.license'

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }
  tasks.withType(ScalaCompile) {
    options.encoding = 'UTF-8'
  }

  apply plugin: 'idea'
  idea {
    module {
      downloadJavadoc = false
      downloadSources = false
    }
  }

  configurations {
    testImplementation.extendsFrom compileOnly
  }

  repositories {
    mavenLocal()
    //maven { url 'https://maven.aliyun.com/nexus/content/groups/public' }
    mavenCentral()
    maven {
      url 'https://oss.sonatype.org/service/local/repositories/snapshots/content/'
    }
  }

  dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    testImplementation group: 'com.github.harbby', name: 'gadtry', version: deps.gadtry
  }

  task clearOutDir(type: Delete) {
    delete project.files('out')
  }
  clean.dependsOn clearOutDir

  jacocoTestReport {
    reports {
      xml.enabled true
      html.enabled true
    }
  }
  check.dependsOn jacocoTestReport

  checkstyle {
    toolVersion '9.2.1'
    showViolations true
  }

  task checkstyle(type: Checkstyle) {
    configFile = file("${rootProject.projectDir}/src/checkstyle/facebook_checks.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**', '**/test/**', '**/build/**', '**/module-info.java'
    classpath = files()
  }
//    assemble.dependsOn 'checkstyle'

  /* 如果依赖版本冲突 则直接失败策略设置*/
  configurations.all {
    resolutionStrategy { failOnVersionConflict() }
  }
  // ./gradlew sylph-runners:sylph-flink:dependencies

  license {
    headerDefinitions {  //see: http://code.mycila.com/license-maven-plugin/#supported-comment-types
      javadoc_style {
        firstLine = "/*"
        endLine = " */"
        beforeEachLine = " * "
        afterEachLine = ""
        firstLineDetectionPattern = "(\\s|\\t)*/\\*.*\$"
        lastLineDetectionPattern = ".*\\*/(\\s|\\t)*\$"
        allowBlankLines = false
        padLines = false
        //skipLine = "//"
        isMultiline = true
      }
    }
    header rootProject.file('src/license/LICENSE-HEADER.txt')
    strictCheck true
    excludes(["**/*.properties", "**/*.sql", "**/*.xml"])
    //include "**/*.java"
  }
  //assemble.dependsOn 'licenseMain','licenseTest'
  //licenseMain.includes
  //license.mapping('javascript', 'JAVADOC_STYLE')

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    javadoc.failOnError = false
  }

//  artifacts {
//    archives sourcesJar, javadocJar
//  }

  javadoc {
    options {
      encoding "UTF-8"
      charSet 'UTF-8'
      author true
      version true
      links "https://github.com/harbby/sylph"
      title "sylph"
    }
  }

  apply plugin: 'signing'
  ext {
    shouldSign = rootProject.hasProperty('mavenUsername')
    mavenUsername = rootProject.hasProperty('mavenUsername') ? rootProject.mavenUsername : ''
    mavenPassword = rootProject.hasProperty('mavenPassword') ? rootProject.mavenPassword : ''
  }

  signing {
    required { shouldSign }
    sign configurations.archives
  }
}
