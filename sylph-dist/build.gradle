configurations.all {
  resolutionStrategy { preferProjectModules() }
}
configurations {
  downloadOnly
}
apply from: "$rootDir/profile-java17.gradle"

dependencies {
  implementation project(':sylph-main')
  downloadOnly group: 'org.apache.hadoop', name: 'hadoop-client', version: deps.hadoop
}

task copyLibs(type: Copy) {
  from(configurations.runtimeClasspath)
  into project.buildDir.path + '/lib'
}
task copyFiles(type: Copy, dependsOn: copyLibs) {
  from('src')
  into project.buildDir
}
task copyHadoopLibs(type: Copy) {
  from(configurations.downloadOnly)
  into project.buildDir.path + '/hadoop-lib'
}

task dist(type: Tar) {
  baseName = rootProject.getName()
  classifier = 'bin'
  extension = 'tgz'
  compression = Compression.GZIP
  def tarpath = baseName + "-${rootProject.version}"

  from(configurations.runtimeClasspath) {
    into tarpath + "/lib"
  }

  from(project.files('src/bin')) {
    into(tarpath + "/bin")
    fileMode = 0755
  }
  from(project.files('src/etc')) {
    into(tarpath + "/etc")
  }

  from(project.files('src/data')) {
    into(tarpath + "/data")
  }

  from(project.files('src/jobs')) {
    into(tarpath + "/jobs")
  }

  from(project.files('src/webapp')) {
    into(tarpath + "/webapp")
  }

  from(project.files('build/modules')) {
    into tarpath + "/modules"
  }

  from(project.files('build/connectors')) {
    into(tarpath + "/connectors")
  }
}
dist.dependsOn copyFiles,copyHadoopLibs


//artifacts {
//  'default' dist
//}

tasks.findByName("uploadArchives")?.setEnabled(false)
//tasks.remove(tasks.findByName("uploadArchives"))
